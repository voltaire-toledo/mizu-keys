#Requires AutoHotkey v2.0
#SingleInstance Force

SqueezeAndPose(screen_percent, resize_percent := 0) {
  ; ╭───────────────────────────────────────────────────────────────────────────────────────────╮
  ; │ SqueezeAndPose: Resize and center the active window to a percentage of the screen size.   │
  ; │ screen_percent: Percentage of the screen size to resize the window to (e.g., 50 for 50%). │
  ; │ resize_percent: Percentage of the screen size to resize the window by (e.g., -5 for 5%    │
  ; │                 smaller, 5 for 5% larger).                                                │
  ; │ If both are zero, do nothing.                                                             │
  ; ╰───────────────────────────────────────────────────────────────────────────────────────────╯
  ; Validate input
  if (screen_percent = 0 && resize_percent = 0)
    return

  ; Get the total Monitor Work Area
  totalWorkAreawidth := SysGet(78) ; Total Work Area Width
  totalWorkAreaHeight := SysGet(79) ; Total Work Area Height
  totalWorkArea := SysGet(80) ; Total Work Area (as an object)

  ; Get the active window handle
  winX := 0, winY := 0, winW := 0, winH := 0
  active_hwnd := WinGetID("A")
  if !active_hwnd {
    MsgBox("No active window handle found!")
    return
  }
  WinGetPos(&winX, &winY, &winW, &winH, active_hwnd)

  ; get the center coordinates of the active window
  center_x := winX + Floor(winW / 2)
  center_y := winY + Floor(winH / 2)

  ; Get the active window's position, size and monitor information
  MonitorCount := MonitorGetCount()
  MonitorPrimary := MonitorGetPrimary()

  ; Find which monitor the window is on and get its work area
  MonitorGetWorkAreaNum := 1
  Loop MonitorCount
  {
    MonitorGet(A_Index, &L, &T, &R, &B)
    if (center_x >= L && center_x <= R && center_y >= T && center_y <= B) {
      MonitorGetWorkAreaNum := A_Index
      break
    }
  }

  MonitorGetWorkArea(MonitorGetWorkAreaNum, &screenX, &screenY, &screenW, &screenH)
  screenW := screenW - screenX
  screenH := screenH - screenY

  if (screen_percent > 0) {
    ; Resize to screen_percent% of monitor, then apply resize_percent
    percent := (screen_percent + resize_percent) / 100
    newW := Round(screenW * percent)
    newH := Round(screenH * percent)
  } else {
    ; Keep window size, but adjust by resize_percent of monitor
    newW := winW + Round(screenW * (resize_percent / 100))
    newH := winH + Round(screenH * (resize_percent / 100))
  }

  newX := screenX + Floor((screenW - newW) / 2)
  newY := screenY + Floor((screenH - newH) / 2)

  ; MsgBox("Handle: " . active_hwnd . " Title: " . WinGetTitle(active_hwnd))
  ; if (WinGetMinMax(active_hwnd) = 1)  ; 1 = Maximized
  WinRestore(active_hwnd) ; WinrRestore tends to keep the window animation smooth
  WinMove(newX, newY, newW, newH, active_hwnd)
}

ResizeWindowBorders(leftDelta := 0, bottomDelta := 0, topDelta := 0, rightDelta := 0) {
  ; ╭────────────────────────────────────────────────────────────────────────────────────────────────╮
  ; │ ResizeWindowBorders: Resize the active window by moving its borders in specified directions.   │
  ; │   leftDelta: Pixels to move the left border (negative to decrease width).                      │
  ; │   bottomDelta: Pixels to move the bottom border (negative to decrease height).                 │
  ; │   topDelta: Pixels to move the top border (negative to decrease height).                       │
  ; │   rightDelta: Pixels to move the right border (negative to decrease width).                    │
  ; ╰────────────────────────────────────────────────────────────────────────────────────────────────╯
  ; Get the active window handle and its current position/size
  winX := 0, winY := 0, winW := 0, winH := 0
  active_hwnd := WinGetID("A")
  if !active_hwnd {
    MsgBox("No active window handle found!")
    return
  }
  WinGetPos(&winX, &winY, &winW, &winH, active_hwnd)

  ; Calculate new position and size
  newX := winX - leftDelta
  newY := winY - topDelta
  newW := winW + leftDelta + rightDelta
  newH := winH + topDelta + bottomDelta

  ; Prevent negative width/height
  if (newW < 1)
    newW := 100
  if (newH < 1)
    newH := 100

  WinRestore(active_hwnd) ; WinrRestore tends to keep the window animation smooth
  WinMove(newX, newY, newW, newH, active_hwnd)
}


; ╭────────────────────────────────────────────────╮
; │ Shortcuts for resizing and positioning windows │
; ╰────────────────────────────────────────────────╯
Capslock & /:: { ; [CapsLock]+[/]: Resize to 70% of the screen size
  SqueezeAndPose(70)
  return
}
CapsLock & [:: { ; [CapsLock]+[Left Square Bracket]: Decrease size by 5% of the screen size
  SqueezeAndPose(0, -5)
  return
}
CapsLock & ]:: { ; [CapsLock]+[Right Square Bracket]: Increase size by 5% of the screen size
  SqueezeAndPose(0, 5)
  return
}

; ╭───────────────────────────────────────────────────────────────────────────────────────────────────╮
; │ Shortcuts for resizing window dimensions by 25 pixels:                                            │
; │ - [CapsLock]+[LShift]+[← ↑ ↓ →]: Decrease window dimensions by moving border in the key direction │
; │ - [CapsLock]+[LCtrl]+[← ↑ ↓ →]: Resize window vertically or horizontally                          │
; │ - [CapsLock]+[← ↑ ↓ →]: Increase window dimensions by moving border in the key direction          │
; ╰───────────────────────────────────────────────────────────────────────────────────────────────────╯
CapsLock & Up:: {
  if GetKeyState("LShift", "P") {
    ResizeWindowBorders(0, 0, -25, 0)   ; [CapsLock]+[LShift]+[↑]: Decrease top border by 25 pixels
  } else if GetKeyState("LCtrl", "P") {
    ResizeWindowBorders(0, 25, 25, 0)   ; [CapsLock]+[LCtrl]+[↑]: Increase top and bottom border by 25 pixels
  } else {
    ResizeWindowBorders(0, 0, 25, 0)    ; [CapsLock]+[↑]: Increase top border by 25 pixels
  }
  return
}
CapsLock & Down:: {
  if GetKeyState("LShift", "P") {
    ResizeWindowBorders(0, -25, 0, 0)     ; [CapsLock]+[LShift]+[↓]: Decrease bottom border by 25 pixels
  } else if GetKeyState("LCtrl", "P") {
    ResizeWindowBorders(0, -25, -25, 0)   ; [CapsLock]+[LCtrl]+[↓]: Decrease top and bottom border by 25 pixels
  } else {
    ResizeWindowBorders(0, 25, 0, 0)      ; [CapsLock]+[↓]: Increase bottom border by 25 pixels
  }
  return
}
CapsLock & Right:: {
  if GetKeyState("LShift", "P") {
    ResizeWindowBorders(0, 0, 0, -25)     ; [CapsLock]+[LShift]+[→]: Decrease right border by 25 pixels
  } else if GetKeyState("LCtrl", "P") {
    ResizeWindowBorders(25, 0, 0, 25)     ; [CapsLock]+[LCtrl]+[→]: Increase left and right border by 25 pixels
  } else {
    ResizeWindowBorders(0, 0, 0, 25)      ; [CapsLock]+[→]: Increase right border by 25 pixels
  }
  return
}
CapsLock & Left:: { 
  if GetKeyState("LShift", "P") {
    ResizeWindowBorders(-25, 0, 0, 0)     ; [CapsLock]+[LShift]+[←]: Decrease left border by 25 pixels
  } else if GetKeyState("LCtrl", "P") {
    ResizeWindowBorders(-25, 0, 0, -25)   ; [CapsLock]+[LCtrl]+[←]: Decrease left border by 25 pixels
  } else {
    ResizeWindowBorders(25, 0, 0, 0)     ; [CapsLock]+[←]: Increase left border by 25 pixels
  }
  return
}